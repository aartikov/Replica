pluginManagement {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        maven {
            url "https://maven.pkg.jetbrains.space/public/p/compose/dev"
        }
    }
}

rootProject.name = 'Replica'

include ':replica-core'
include ':replica-algebra'
include ':replica-android-network'
include ':replica-view-model'
include ':replica-decompose'
include ':replica-devtools'
include ':replica-devtools-noop'
include ':replica-devtools-client'
include ':replica-devtools-dto'
include ':advanced-sample'
include ':simple-sample'

dependencyResolutionManagement {
    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://maven.pkg.jetbrains.space/public/p/compose/dev'
        }
    }

    versionCatalogs {
        libs {
            // Kotlin
            library('kotlin-datetime', 'org.jetbrains.kotlinx', 'kotlinx-datetime').version('0.4.0')

            version('coroutines', '1.6.4')
            library('coroutines-core', 'org.jetbrains.kotlinx', 'kotlinx-coroutines-core').versionRef('coroutines')
            library('coroutines-android', 'org.jetbrains.kotlinx', 'kotlinx-coroutines-android').versionRef('coroutines')

            // Android
            library('android-desugar', 'com.android.tools', 'desugar_jdk_libs').version('1.1.5')
            version('lifecycle', '2.5.1')
            library('androidx-lifecycle-process', 'androidx.lifecycle', 'lifecycle-process').versionRef('lifecycle')
            library('lifecycle', 'androidx.lifecycle', 'lifecycle-common').versionRef('lifecycle')
            library('viewModel', 'androidx.lifecycle', 'lifecycle-viewmodel-ktx').versionRef('lifecycle')

            // UI
            version('compose', '1.2.0-rc03')
            version('composeCompiler', '1.2.0')
            library('compose-runtime', 'androidx.compose.runtime', 'runtime').versionRef('compose')
            library('compose-ui', 'androidx.compose.ui', 'ui').versionRef('compose')
            library('compose-material', 'androidx.compose.material', 'material').versionRef('compose')
            library('compose-preview', 'androidx.compose.ui', 'ui-tooling-preview').versionRef('compose')
            library('compose-tooling', 'androidx.compose.ui', 'ui-tooling').versionRef('compose')
            library('activity-compose', 'androidx.activity', 'activity-compose').version('1.5.0')
            library('appcompat', 'androidx.appcompat', 'appcompat').version('1.5.0')
            library('fragment', 'androidx.fragment', 'fragment-ktx').version('1.5.2')
            library('recyclerView', 'androidx.recyclerview', 'recyclerview').version('1.2.1')
            library('swipeRefresh', 'androidx.swiperefreshlayout', 'swiperefreshlayout').version('1.1.0')
            library('material', 'com.google.android.material', 'material').version('1.6.1')
            library('splashscreen', 'androidx.core', 'core-splashscreen').version('1.0.0-rc01')
            version('accompanist', '0.24.13-rc')
            library('accompanist-systemuicontroller', 'com.google.accompanist', 'accompanist-systemuicontroller').versionRef('accompanist')
            library('accompanist-swiperefresh', 'com.google.accompanist', 'accompanist-swiperefresh').versionRef('accompanist')
            bundle('accompanist', ['accompanist-systemuicontroller', 'accompanist-swiperefresh'])
            version('coil', '2.2.0')
            library('coil', 'io.coil-kt', 'coil').versionRef('coil')
            library('coilCompose', 'io.coil-kt', 'coil-compose').versionRef('coil')

            // Architecture
            library('sesame-localizedString', 'com.github.aartikov', 'sesame-localized-string').version('1.5.0')

            version('decompose', '1.0.0-beta-04')
            library('decompose-core', 'com.arkivanov.decompose', 'decompose').versionRef('decompose')
            library('decompose-compose', 'com.arkivanov.decompose', 'extensions-compose-jetpack').versionRef('decompose')
            bundle('decompose', ['decompose-core', 'decompose-compose'])

            // Serialization
            version('serialization', '1.3.3')
            library('serialization-core', 'org.jetbrains.kotlinx', 'kotlinx-serialization-core').versionRef('serialization')
            library('serialization-json', 'org.jetbrains.kotlinx', 'kotlinx-serialization-json').versionRef('serialization')

            // Network
            library('retrofit-core', 'com.squareup.retrofit2', 'retrofit').version('2.9.0')
            library('retrofit-converter-serialization', 'com.jakewharton.retrofit', 'retrofit2-kotlinx-serialization-converter').version('0.8.0')
            library('okhttp-logging', 'com.squareup.okhttp3', 'logging-interceptor').version('4.9.2')

            // DI
            library('koin-android', 'io.insert-koin', 'koin-android').version('3.2.0')

            // Debugging
            library('timber', 'com.jakewharton.timber', 'timber').version('5.0.1')

            version('hyperion', '0.9.34')
            version('hyperionAddons', '0.3.3')
            library('hyperion-core', 'com.willowtreeapps.hyperion', 'hyperion-core').versionRef('hyperion')
            library('hyperion-recorder', 'com.willowtreeapps.hyperion', 'hyperion-recorder').versionRef('hyperion')
            library('hyperion-crash', 'com.willowtreeapps.hyperion', 'hyperion-crash').versionRef('hyperion')
            library('hyperion-disk', 'com.willowtreeapps.hyperion', 'hyperion-disk').versionRef('hyperion')
            library('hyperion-addons-networkEmulation', 'me.nemiron.hyperion', 'network-emulation').versionRef('hyperionAddons')
            library('hyperion-addons-chucker', 'me.nemiron.hyperion', 'chucker').versionRef('hyperionAddons')
            bundle('hyperion',
                    ['hyperion-core', 'hyperion-recorder', 'hyperion-crash', 'hyperion-disk',
                     'hyperion-addons-networkEmulation', 'hyperion-addons-chucker']
            )
            library('chucker', 'com.github.chuckerteam.chucker', 'library').version('3.5.2')

            // Testing
            library('junit', 'junit', 'junit').version('4.13.2')
            library('coroutines.test', 'org.jetbrains.kotlinx', 'kotlinx-coroutines-test').versionRef('coroutines')

            // Ktor
            version('ktor', '2.0.3')
            library('ktor-server-core', 'io.ktor', 'ktor-server-core').versionRef('ktor')
            library('ktor-server-netty', 'io.ktor', 'ktor-server-netty').versionRef('ktor')
            library('ktor-server-websockets', 'io.ktor', 'ktor-server-websockets').versionRef('ktor')
            bundle('ktor-server', ['ktor-server-core', 'ktor-server-netty', 'ktor-server-websockets'])
            library('ktor-client-core', 'io.ktor', 'ktor-client-core').versionRef('ktor')
            library('ktor-client-websockets', 'io.ktor', 'ktor-client-websockets').versionRef('ktor')
            bundle('ktor-client', ['ktor-client-core', 'ktor-client-websockets'])
        }
    }
}